       PROCESS  OPTIONS.
       IDENTIFICATION DIVISION.
       PROGRAM-ID. VERIFY.
         AUTHOR. PROGRAMMER NAME.
         INSTALLATION. TORONTO LABORATORY.
         DATE-WRITTEN. JANUARY 1, 1995.
         DATE-COMPILED.
       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
         SOURCE-COMPUTER. IBM-I.
         OBJECT-COMPUTER. IBM-I.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT FILE1 ASSIGN TO PRINTER-QSYSPRT
                  ORGANIZATION IS SEQUENTIAL.
       DATA DIVISION.
       FILE SECTION.
       FD  FILE1
           RECORD CONTAINS 39 CHARACTERS.
       01  REC-1 PIC X(39).
       WORKING-STORAGE SECTION.
       01  PRINT-LINE  PIC X(39) VALUE
           "ILE COBOL INSTALLATION IS VERIFIED.".
       77 ERROR-COUNT PIC 9(3) VALUE ZEROES.
       01 TEST-VARS.
          05 A PIC 99 USAGE BINARY.
          05 B PIC 99 USAGE COMP-3.
          05 C PIC 999999 USAGE PACKED-DECIMAL.
          05 D PIC S99 USAGE BINARY.
       PROCEDURE DIVISION.
       TEST1-INIT.
           MOVE 5 TO A.
           MOVE 2 TO B.
           MOVE -5 TO D.
       TEST-COMPUTE.
           COMPUTE C = A * B + D.
           IF A NOT = 5 OR B NOT = 2 OR D NOT = -5 OR C NOT = 5
            DISPLAY "COMPUTE FAILED FOR THE FOLLOWING:"
            DISPLAY "A = 5 =" A
            DISPLAY "B = 2 =" B
            DISPLAY "D = -5 =" D
            DISPLAY "Compute: 5 =" C
            ADD 1 TO ERROR-COUNT.
       TEST-ADD.
           ADD A B A B GIVING C.
           IF C NOT = 14
            DISPLAY "ADD FAILED FOR THE FOLLOWING:"
            DISPLAY "Add: 14 =" C
            ADD 1 TO ERROR-COUNT.
       TEST-SUBTRACT.
           SUBTRACT B D FROM A GIVING C.
           IF C NOT = 8
            DISPLAY "SUBTRACT FAILED FOR THE FOLLOWING:"
            DISPLAY "Subtract: 8 =" C
            ADD 1 TO ERROR-COUNT.
       TEST-DIVIDE.
           DIVIDE A BY B GIVING C.
           IF C NOT = 2 THEN
            DISPLAY "DIVIDE FAILED FOR THE FOLLOWING:"
            DISPLAY "Divide: 2 =" C
            ADD 1 TO ERROR-COUNT.
       FINISHED-NOW.
           IF ERROR-COUNT = 0 THEN
            OPEN OUTPUT FILE1
            WRITE REC-1 FROM PRINT-LINE
            CLOSE FILE1.
           STOP RUN.